apply plugin: 'com.android.application'

// Keystore
def keystorePropertiesFile = file("/home/zet/.gradle/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "tech.zettervall.notes"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true // Required for testing because of Logging
            includeAndroidResources = true // Required for Robolectric
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding.enabled true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.preference:preference:1.1.0"

    // JUnit4
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"

    // Espresso
    def espressoVersion = "3.2.0"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.3"

    // Room
    def roomVersion = "2.2.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Lifecycle
    def lifeCycleVersion = "2.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifeCycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-common-java8:$lifeCycleVersion"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // Paging
    implementation "androidx.paging:paging-runtime:2.1.0"

    // Parceler
    def parcelerVersion = "1.1.12"
    implementation "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    // Zoomage (ImageView Pinch Zoom)
    implementation "com.jsibbold:zoomage:1.3.0"
}
